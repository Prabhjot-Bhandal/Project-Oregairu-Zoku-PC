label E2_YUK_01:
    scene khstationA with Fade(1.0, 0.5, 1.0)
    play music "audio/bgm/BGM06.ogg"
    #walking sfx
    play footsteps "audio/sfx/SE00/SE00_18L.ogg"
    $renpy.pause(delay=2.0, hard=False)
    stop footsteps fadeout 1
    show komachi coat mid_center pout at center with dissolve:
        xoffset -75 yoffset 75
    window auto show dissolve
    voice "audio/voice/E2/YUK/01/KO/KO000.ogg"
    komachi "Onii-chan. Sorry for making you come out in the cold for Komachi."
    voice "audio/voice/E2/YUK/01/HA/HA000.ogg"
    hachiman  "It's not good to brood over your exams after all. A change of pace is necessary."
    hachiman "(This coldness is absolutely nothing if it means I can make Komachi feel better!)"
    show komachi surprised with dissolve
    voice "audio/voice/E2/YUK/01/KO/KO001.ogg"
    komachi "...Ah!"
    voice "audio/voice/E2/YUK/01/HA/HA001.ogg"
    hachiman  "...Hm?"
    show komachi happy with dissolve
    voice "audio/voice/E2/YUK/01/KO/KO002.ogg"
    komachi "It's Yukino-san! Hey! Yukino-san!"
    hide komachi with dissolve
    show yukino coat mid_center vhappy at left with dissolve:
        xoffset 25 yoffset 75
    voice "audio/voice/E2/YUK/01/YK/YK000.ogg"
    yukino  "Komachi-san... and, Hikigaya-kun."
    show komachi coat mid_left vhappy at right with dissolve:
        xoffset -275 yoffset 75
    voice "audio/voice/E2/YUK/01/KO/KO003.ogg"
    komachi "Good afternoon!"
    voice "audio/voice/E2/YUK/01/HA/HA002.ogg"
    hachiman  "Yo."
    voice "audio/voice/E2/YUK/01/YK/YK001.ogg"
    yukino "It's a coincidence seeing you here."
    voice "audio/voice/E2/YUK/01/HA/HA003.ogg"
    hachiman  "I figured I'd bring Komachi to eat something sweet as a change of pace while in the middle of her exam studies."
    show yukino concerned with dissolve
    voice "audio/voice/E2/YUK/01/YK/YK002.ogg"
    yukino "It's just around the corner so it makes sense. Let's see... At this point, I think repeating over past questions will build your confidence."
    show komachi pout with dissolve
    voice "audio/voice/E2/YUK/01/KO/KO004.ogg"
    komachi  "I am doing that, but still..."
    show komachi surprised2 with dissolve
    voice "audio/voice/E2/YUK/01/KO/KO005.ogg"
    komachi  "Ah..!"
    show komachi sad with dissolve
    voice "audio/voice/E2/YUK/01/KO/KO006.ogg"
    komachi  "But the results from the mock exam made Komachi lose her confidence... but, the exam is coming up really soon too... Komachi doesn't know what to do!"
    voice "audio/voice/E2/YUK/01/HA/HA004.ogg"
    hachiman  "I told her being all anxious now is pointless though."
    show yukino vhappy with dissolve
    voice "audio/voice/E2/YUK/01/YK/YK003.ogg"
    yukino "That's true. Komachi-san, I understand how you feel, but you should relax first and calm down."
    voice "audio/voice/E2/YUK/01/KO/KO007.ogg"
    komachi  "Yukino-san! But Komachi can't get out of this negative spiral that Komachi is doomed to be in."
    show komachi angry with dissolve
    voice "audio/voice/E2/YUK/01/KO/KO008.ogg"
    komachi  "Um...! If it isn't a bother, can't you please help Komachi?"
    show yukino surprised with dissolve
    voice "audio/voice/E2/YUK/01/YK/YK004.ogg"
    yukino "... Me?"
    show komachi sad with dissolve
    voice "audio/voice/E2/YUK/01/KO/KO009.ogg"
    komachi  "I-it is a bother after all, isn't it..."
    voice "audio/voice/E2/YUK/01/HA/HA005.ogg"
    hachiman  "That's right, Komachi. Yukinoshita probably has things to do too. I'll help you if you're cornered."
    show komachi mid_center unimpressed at right with dissolve:
        xoffset -200 yoffset 75
    voice "audio/voice/E2/YUK/01/KO/KO010.ogg"
    komachi  "But Onii-chan, you're bad at everything except Japanese, and your way of teaching is subtly distorted."
    hachiman "(Distorted... She just can't say I'm self-concious...)"
    show komachi mid_left pout1 at right with dissolve:
        xoffset -275 yoffset 75
    voice "audio/voice/E2/YUK/01/KO/KO011.ogg"
    komachi  "Yukino-san! Is it no good?"
    show yukino pout with dissolve
    voice "audio/voice/E2/YUK/01/YK/YK005.ogg"
    yukino "N-no... It's not like that..."
    show komachi vhappy
    voice "audio/voice/E2/YUK/01/KO/KO012.ogg"
    komachi  "Then please come to our house!"
    voice "audio/voice/E2/YUK/01/YK/YK006.ogg"
    show yukino concerned with dissolve
    yukino "Um... Won't it be a bother to your family?"
    show komachi happy with dissolve
    voice "audio/voice/E2/YUK/01/KO/KO013.ogg"
    komachi  "Both of our parents work and come home late so it's completely okay! Kaa-kun, our cat, is there too!"
    stop music
    show yukino surprised with dissolve
    voice "audio/voice/E2/YUK/01/YK/YK007.ogg"
    yukino "...!?"
    show komachi vhappy with dissolve
    play music "audio/bgm/BGM44.ogg"
    voice "audio/voice/E2/YUK/01/KO/KO014.ogg"
    komachi  "His winter hair is so fluffy! I'd love it if you could meet him!"
    hachiman "(Th-there it is! The cat deathblow attack! She may be my younger sister, but she's so schemeful...)"
    show yukino vhappy with dissolve
    voice "audio/voice/E2/YUK/01/YK/YK008.ogg"
    yukino "Winter hair... Fluffy..."
    show komachi mid_center vhappy at right with dissolve:
        xoffset -200 yoffset 75
    voice "audio/voice/E2/YUK/01/KO/KO015.ogg"
    komachi  "Yep! Right, Onii-chan?"
    voice "audio/voice/E2/YUK/01/HA/HA006.ogg"
    hachiman  "Well... it's not a lie..."
    show komachi mid_left vhappy at right with dissolve:
        xoffset -275 yoffset 75
    voice "audio/voice/E2/YUK/01/KO/KO016.ogg"
    komachi  "If Yukino-san comes over, I'm sure Komachi's stress will fly away!"
    show yukino blush with dissolve
    voice "audio/voice/E2/YUK/01/YK/YK009.ogg"
    yukino "I-I might not be of any use, but if that's how it is then..."
    window auto hide dissolve
    stop voice
    scene skyA with dissolve
    window auto show dissolve
    voice "audio/voice/E2/YUK/01/KO/KO017.ogg"
    komachi  "Hooray! Then let's go to the Hikigaya household immediately!"
    voice "audio/voice/E2/YUK/01/YK/YK010.ogg"
    yukino "A-alright..."
    window auto hide dissolve
    stop voice
    stop music fadeout 0.5
    jump E2_YUK_02

label E2_YUK_02:
    scene outsideA with Fade(1.0, 0.5, 1.0)
    play music "audio/bgm/BGM06.ogg"
    window auto show dissolve
    "As if her stress just went off elsewhere, Komachi brought a nervous Yukinoshita all the way home."
    "As for me, because I'd never invited someone home before, I was pretty nervous myself."
    voice "audio/voice/E2/YUK/02/KO/KO000.ogg"
    komachi "Yukino-san! It's over here!"
    voice "audio/voice/E2/YUK/02/YK/YK000.ogg"
    yukino "O-okay..."
    window auto hide dissolve
    scene houseA with Fade(1.0, 0.5, 1.0)
    show komachi home mid_left vhappy at right with dissolve:
        xoffset -270 yoffset 75
    window auto show dissolve
    voice "audio/voice/E2/YUK/02/KO/KO001.ogg"
    komachi "Come in! Please make yourself comfortable! I'll go make the tea now!"
    show yukino home mid_center pout at left with dissolve:
        xoffset 190 yoffset 75
    voice "audio/voice/E2/YUK/02/YK/YK001.ogg"
    yukino "T-thank you..."
    hide komachi with dissolve
    voice "audio/voice/E2/YUK/02/HA/HA000.ogg"
    hachiman "W-well, just sit on the sofa or wherever."
    voice "audio/voice/E2/YUK/02/YK/YK002.ogg"
    yukino "Okay..."
    voice "audio/voice/E2/YUK/02/WC/WC000.ogg"
    kamakura "Meoww~"
    show yukino surprised with dissolve
    voice "audio/voice/E2/YUK/02/YK/YK003.ogg"
    yukino "!!!"
    show yukino blush with dissolve
    voice "audio/voice/E2/YUK/02/YK/YK004.ogg"
    yukino "... Ka,Kamakura... Kaa-kun..."
    voice "audio/voice/E2/YUK/02/HA/HA001.ogg"
    hachiman "On the verge of becoming a ball of hair, right?"
    show yukino avoid with dissolve
    voice "audio/voice/E2/YUK/02/YK/YK005.ogg"
    yukino "He's indeed very fluffy... Um... is it okay... to pet him?"
    voice "audio/voice/E2/YUK/02/KO/KO002.ogg"
    komachi "Please play with him! Kaa-kun will be happy too!"
    voice "audio/voice/E2/YUK/02/YK/YK006.ogg"
    yukino "R-really? Then..."
    hide yukino with dissolve
    voice "audio/voice/E2/YUK/02/WC/WC001.ogg"
    kamakura "Meow~"
    voice "audio/voice/E2/YUK/02/YK/YK007.ogg"
    yukino "...Meow..."
    hachiman "(She's concentrating on the cat... Okay I'm gonna pretend I didn't hear anything... I heard absolutely nothing!)"
    window auto hide dissolve
    scene houseA
    show komachi home far_center vhappy at right:
        xoffset -280 yoffset 75
    with Fade(2.0, 0.5, 0.5)
    window auto show dissolve
    voice "audio/voice/E2/YUK/02/KO/KO003.ogg"
    komachi "Yukino-san! The tea is ready!"
    show yukino home mid_center vhappy at left with dissolve:
        xoffset 25 yoffset 75
    voice "audio/voice/E2/YUK/02/YK/YK008.ogg"
    yukino "Ah...! Thank you, Komachi-san."
    hide komachi with dissolve
    show komachi home mid_left vhappy at center with dissolve:
        xoffset 250 yoffset 75
    voice "audio/voice/E2/YUK/02/HA/HA002.ogg"
    hachiman "Oh? Hey, Komachi. Where's mine?"
    show komachi surprised with dissolve
    voice "audio/voice/E2/YUK/02/KO/KO004.ogg"
    komachi "Eh?"
    voice "audio/voice/E2/YUK/02/HA/HA003.ogg"
    hachiman "Eh?"
    voice "audio/voice/E2/YUK/02/HA/HA004.ogg"
    hachiman "No, uh... Where's my tea?"
    show komachi home mid_center surprised at center with dissolve:
        xoffset 170 yoffset 75
    voice "audio/voice/E2/YUK/02/KO/KO005.ogg"
    komachi "Eh, there isn't any."
    voice "audio/voice/E2/YUK/02/HA/HA005.ogg"
    hachiman "...!!!"
    show komachi pout with dissolve
    voice "audio/voice/E2/YUK/02/KO/KO006.ogg"
    komachi "I mean, I thought you wouldn't want one because you always drink coffee... Onii-chan, you drink black tea?"
    show yukino happy with dissolve
    voice "audio/voice/E2/YUK/02/YK/YK009.ogg"
    yukino "He drinks it often in the club room... Did you possibly dislike black tea...?"
    voice "audio/voice/E2/YUK/02/HA/HA006.ogg"
    hachiman "No, I do like black tea. But, well, coffee is easier at home. Also, tea doesn't taste that good when I make it."
    show yukino pout with dissolve
    voice "audio/voice/E2/YUK/02/YK/YK010.ogg"
    yukino "R-really... It's okay then..."
    show komachi happy with dissolve
    voice "audio/voice/E2/YUK/02/KO/KO007.ogg"
    komachi "Onii-chan. There's some black tea left if you want it? Though it won't compare to Yukino-san's black tea."
    voice "audio/voice/E2/YUK/02/HA/HA007.ogg"
    hachiman "Hahaha, what are you saying, Komachi? The moment I know it's made by you, its taste is world class. Onii-chan will commercialise the tea made by my imouto as \"imout-tea\"."
    show komachi unimpressed with dissolve
    voice "audio/voice/E2/YUK/02/KO/KO008.ogg"
    komachi "Yeah, I really wish you'd stop saying those gross things in front of other people."
    show yukino stare with dissolve
    voice "audio/voice/E2/YUK/02/YK/YK011.ogg"
    yukino "So it's okay if it's not in front of other people..."
    show komachi home mid_left surprised with dissolve:
        xoffset 250 yoffset 75
    voice "audio/voice/E2/YUK/02/KO/KO009.ogg"
    komachi "I'm sorry you had to see something so embarassing..."
    voice "audio/voice/E2/YUK/02/HA/HA008.ogg"
    hachiman "Even I'm a little embarassed."
    show yukino vhappy with dissolve
    voice "audio/voice/E2/YUK/02/YK/YK012.ogg"
    yukino "Not at all. You and Komachi-san really get along."
    voice "audio/voice/E2/YUK/02/HA/HA009.ogg"
    hachiman "Hmph, of course."
    show komachi unimpressed with dissolve
    voice "audio/voice/E2/YUK/02/KO/KO010.ogg"
    komachi "No, it's not like that at all though."
    voice "audio/voice/E2/YUK/02/HA/HA010.ogg"
    hachiman "...!?!?"
    voice "audio/voice/E2/YUK/02/YK/YK013.ogg"
    yukino "You do get along."
    show komachi vhappy with dissolve
    voice "audio/voice/E2/YUK/02/KO/KO011.ogg"
    komachi "Well even if he's like this, he is my only brother after all. Anyway, teatime in the club sounds so nice! What else do you do?"
    "And so Komachi was eagerly asking barrages of questions about the Service Club and Sobu High School while Yukinoshita answered them with a smile."
    "It was like she was under the illusion our high school life was sparkling. And then I realised..."
    window auto hide dissolve
    stop music fadeout 0.5
    $renpy.pause(delay=0.5, hard=True)
    show white with Fade(0.25, 0.0, 0.5, color="#fff")
    scene clubroomB
    show yukino school_sunset mid_center vhappy at right:
        xoffset -105 yoffset 75
    show hiratsuka school_light_sunset mid_center happy at left:
        xoffset -20 yoffset 75
    show yui school_sunset mid_right vhappy at center:
        xoffset -110 yoffset 75
    with Fade(0, 0.0, 0.5, color="#fff")
    play music "audio/bgm/BGM19.ogg"
    $renpy.pause(delay=1.0, hard=True)
    scene white with Fade(0.25, 0.0, 0.5, color="#fff")
    scene scouncilroomB
    show yukino school_sunset mid_center vhappy at center:
        xoffset -100 yoffset 75
    show yui school_sunset mid_right vhappy at left:
        xoffset -105 yoffset 75
    show iroha school_sunset mid_left vhappy at right:
        xoffset -195 yoffset 75
    with Fade(0, 0.0, 0.5, color="#fff")
    window auto show dissolve
    "The people who appeared in Yukinoshita's stories consisted of only me, Yuigahama, Isshiki, the Service Club, and the very few people connected to it."
    window auto hide dissolve
    scene white with Fade(0.25, 0.0, 0.5, color="#fff")
    scene classroomA with Fade(0, 0.0, 0.5, color="#fff")
    $renpy.pause(delay=1.0, hard=True)
    scene hallwayA with dissolve
    $renpy.pause(delay=1.0, hard=True)
    scene fieldA with dissolve
    $renpy.pause(delay=1.0, hard=True)
    scene rooftopA with dissolve
    $renpy.pause(delay=1.0, hard=True)
    scene gatesA with dissolve
    $renpy.pause(delay=1.0, hard=True)
    window auto show dissolve
    "There wasn't any mention of the masses that surrounded us, or the ill wills and the loneliness."
    "It could have been Yukinoshita's consideration of Komachi who was about to take her exams. But to me those were-"
    window auto hide dissolve
    stop music fadeout 0.5
    scene white with Fade(0.25, 0.0, 0.5, color="#fff")
    scene houseA
    show komachi home mid_left vhappy at right:
        xoffset -270 yoffset 75
    show yukino home mid_center vhappy at left:
        xoffset 25 yoffset 75
    with Fade(0, 0.0, 0.5, color="#fff")
    play music "audio/bgm/BGM10.ogg"
    window auto show dissolve
    hachiman "(Oh, my cup's empty. So are theirs. Alright...)"
    show yukino happy
    show komachi home mid_center happy at right:
        xoffset -200 yoffset 75
    with dissolve
    voice "audio/voice/E2/YUK/02/KO/KO012.ogg"
    komachi "Hm? Onii-chan? What's up?"
    voice "audio/voice/E2/YUK/02/HA/HA011.ogg"
    hachiman "I'll make some more black tea. You need a refill right?"
    show komachi vhappy with dissolve
    voice "audio/voice/E2/YUK/02/KO/KO013.ogg"
    komachi "You're gonna do it for us? Thanks!"
    show yukino vhappy with dissolve
    voice "audio/voice/E2/YUK/02/YK/YK014.ogg"
    yukino "To think I could drink black tea made by Hikigaya-kun. It was worth coming. Let's see how you do..."
    voice "audio/voice/E2/YUK/02/HA/HA012.ogg"
    hachiman "It's just a teabag so it puts me on the spot if you're expecting something..."
    hachiman "(Well, it's fine since it looks like those two are having fun. I'm sorry to Yukinoshita, but it seems Komachi's been able to take her mind off of things.)"
    window auto hide dissolve
    stop music fadeout 1.0
    scene houseB
    show yukino home_sunset mid_center vhappy at left:
        xoffset 25 yoffset 75
    show komachi home_sunset mid_left blush at right:
        xoffset -270 yoffset 75
    with Fade(2.0, 0.5, 1.0)
    play music "audio/bgm/BGM41.ogg"
    window auto show dissolve
    voice "audio/voice/E2/YUK/02/KO/KO014.ogg"
    komachi "Yukino-san, Komachi is brimming with confidence!"
    voice "audio/voice/E2/YUK/02/YK/YK015.ogg"
    yukino "Really, that is a relief. Komachi-san, you'll be fine if you calm down and look at the questions properly."
    show komachi vhappy with dissolve
    voice "audio/voice/E2/YUK/02/KO/KO015.ogg"
    komachi "If Komachi is commended by you, Komachi is brimming with even more courage!"
    show komachi happy with dissolve
    voice "audio/voice/E2/YUK/02/KO/KO016.ogg"
    komachi "Ah, Komachi will start making dinner now."
    show yukino surprised with dissolve
    voice "audio/voice/E2/YUK/02/YK/YK016.ogg"
    yukino "Ah... I'm sorry for intruding for so long. Then I'll be going now."
    show komachi pout with dissolve
    voice "audio/voice/E2/YUK/02/KO/KO017.ogg"
    komachi "Ehh? Please eat with us too!"
    show yukino pout with dissolve
    voice "audio/voice/E2/YUK/02/YK/YK017.ogg"
    yukino "But..."
    show komachi sad with dissolve
    voice "audio/voice/E2/YUK/02/KO/KO018.ogg"
#font size should be smaller for the following line. Need to fix the line spacing on this. No sentence breaks:
    komachi "{size=30}Komachi is always eating with just onii-chan. It's so lonely sometimes,{size=45} {/size}it's so painful when it's just the two of us always only replies{size=45} {/size}\"It's normal\" or \"It's the usual\" he doesn't realise when I change the seasoning. There are times when{size=45} {/size}I just wish there was someone else {/size}"
    voice sustain
    komachi "{size=30}with us...!{/size}when{size=45} {/size}"
    show yukino sad with dissolve
    voice "audio/voice/E2/YUK/02/YK/YK018.ogg"
    yukino "They're like a mature housewife's problems. My heart hurts a little just listening..."
    voice "audio/voice/E2/YUK/02/HA/HA013.ogg"
    hachiman "Hey, my heart is hurting quite a lot too..."
    show komachi vhappy with dissolve
    voice "audio/voice/E2/YUK/02/KO/KO019.ogg"
    komachi "Which. Is. Why. If it's okay with you, couldn't you please eat dinner with us? Right, Onii-chan?"
    voice "audio/voice/E2/YUK/02/HA/HA014.ogg"
    hachiman "Y-Yeah... If we're not bothering you..."
    show yukino surprised with dissolve
    voice "audio/voice/E2/YUK/02/YK/YK019.ogg"
    yukino "A-A bother, not at all... Then I'll take your offer..."
    voice "audio/voice/E2/YUK/02/KO/KO020.ogg"
    show komachi vhappy2 with dissolve
    komachi "Hooray! Komachi is way too happy today! I have to wave my arms around!"
    show yukino avoid
    show komachi vhappy
    with dissolve
    voice "audio/voice/E2/YUK/02/YK/YK020.ogg"
    yukino "You're exaggerating, Komachi-san. Um..."
    show yukino blush with dissolve
    voice "audio/voice/E2/YUK/02/YK/YK021.ogg"
    yukino "If it's okay, would it be alright if you let me make it? How about it, Hikigaya-kun?"
    voice "audio/voice/E2/YUK/02/HA/HA015.ogg"
    hachiman "Eh, me?"
    show yukino avoid with dissolve
    voice "audio/voice/E2/YUK/02/YK/YK022.ogg"
    yukino "Komachi-san is studying for her exams, and you're free so I wondered..."
    voice "audio/voice/E2/YUK/02/HA/HA016.ogg"
    hachiman "It is okay... I'm actually free after all..."
    window auto hide dissolve
    stop voice
    stop music fadeout 1.0
    scene yukinocookevent with Fade(1.0, 0.5, 1.0)
    play music "audio/bgm/BGM18.ogg"
    play ambient "audio/sfx/SE01/SE01_17.ogg" #loop #seems to not be looped.
    window auto show dissolve
    voice "audio/voice/E2/YUK/02/YK/YK023.ogg"
    yukino "Hikigaya-kun, could you pass the mixed salt? The basil and lemongrass too."
    voice "audio/voice/E2/YUK/02/HA/HA017.ogg"
    hachiman "Eh, do we have seasonings like that in our house? This? No, is it this?"
    voice "audio/voice/E2/YUK/02/YK/YK024.ogg"
    yukino "It's on that shelf. This is your own house kitchen. You should at least know where things are."
    voice "audio/voice/E2/YUK/02/HA/HA018.ogg"
    hachiman "W-well that's true... As you say..."
    hachiman "(T-true... It's such a sound argument I can't make a rebuttal...)"
    voice "audio/voice/E2/YUK/02/KO/KO021.ogg"
    komachi "Onii-chan is! Obediently listening to! Yukino-san! Cooking together! I wanted to show our parents this..."
    voice "audio/voice/E2/YUK/02/HA/HA019.ogg"
    hachiman "They're alive; our parents are alive."
    voice "audio/voice/E2/YUK/02/YK/YK025.ogg"
    yukino "Ah..."
    voice "audio/voice/E2/YUK/02/HA/HA020.ogg"
    hachiman "Ah? What's wrong?"
    voice "audio/voice/E2/YUK/02/YK/YK026.ogg"
    yukino "Ah no, um... Won't your parents feel uncomfortable if I used their kitchen? I might have been too intrusive..."
    voice "audio/voice/E2/YUK/02/HA/HA021.ogg"
    hachiman "Ahh, don't worry, don't worry. Our parents don't concern themselves over that."
    voice "audio/voice/E2/YUK/02/YK/YK027.ogg"
    yukino "...I'm concerned though..."
    voice "audio/voice/E2/YUK/02/HA/HA022.ogg"
    hachiman "Ahh, well this is someone else's house. I guess you'd refrain a bit."
    voice "audio/voice/E2/YUK/02/YK/YK028.ogg"
    yukino "That's not what I meant... It doesn't matter. Anyway, Hikigaya-kun. I'm going to start serving, so pass me the can of cat food."
    voice "audio/voice/E2/YUK/02/HA/HA023.ogg"
    hachiman "Here, the can right... Wait, cat food? You're gonna use it in your cooking?"
    voice "audio/voice/E2/YUK/02/YK/YK029.ogg"
    yukino "No, it's for Kamakura. He might get a cut if you just give him the can."
    voice "audio/voice/E2/YUK/02/HA/HA024.ogg"
    hachiman "S-sure... You're kind to cats..."
    voice "audio/voice/E2/YUK/02/WC/WC002.ogg"
    kamakura "Meow~"
    window auto hide dissolve
    stop music fadeout 1.0
    stop sound
    stop ambient
    jump E2_YUK_03

label E2_YUK_03:
    scene neighborhoodC
    show yukino coat_shade mid_center happy at center:
        xoffset -105 yoffset 75
    with Fade(1.0, 0.5, 1.0)
    play music "audio/bgm/BGM11.ogg"
    window auto show dissolve
    voice "audio/voice/E2/YUK/03/HA/HA000.ogg"
    hachiman "Um... I'm sorry about today. Even though you were probably busy too..."
    show yukino vhappy with dissolve
    voice "audio/voice/E2/YUK/03/YK/YK000.ogg"
    yukino "Not at all. The same for me, I'm sorry for staying over so long."
    show yukino stare with dissolve
    voice "audio/voice/E2/YUK/03/YK/YK001.ogg"
    yukino "Komachi-san is a good girl. I wonder if that's what it's like to have a little sister..."
    voice "audio/voice/E2/YUK/03/HA/HA001.ogg"
    hachiman "Well that's because Komachi is the world's best little sister. That skill in sneakiness isn't similar to me though."
    show yukino vhappy with dissolve
    voice "audio/voice/E2/YUK/03/YK/YK002.ogg"
    yukino "Oh, you're aware of that."
    voice "audio/voice/E2/YUK/03/HA/HA002.ogg"
    hachiman "That wasn't a compliment, was it."
    show yukino happy with dissolve
    voice "audio/voice/E2/YUK/03/YK/YK003.ogg"
    yukino "Who knows."
    show yukino vhappy with dissolve
    voice "audio/voice/E2/YUK/03/YK/YK004.ogg"
    yukino "Komachi will definitely pass. I'm looking forward to spring."
    voice "audio/voice/E2/YUK/03/HA/HA003.ogg"
    hachiman "If it's a commendation from you, then it's a relief."
    voice "audio/voice/E2/YUK/03/YK/YK005.ogg"
    yukino "Also... it's relieving just looking at you two siblings."
    show yukino stare with dissolve
    voice "audio/voice/E2/YUK/03/YK/YK006.ogg"
    yukino "I feel like I understand why Komachi-san can be that way."
    voice "audio/voice/E2/YUK/03/HA/HA004.ogg"
    hachiman "True. I'm so glad she isn't similar to me."
    voice "audio/voice/E2/YUK/03/HA/HA005.ogg"
    hachiman "There's something good about siblings not being similar to each other, right?"
    show yukino surprised with dissolve
    voice "audio/voice/E2/YUK/03/YK/YK007.ogg"
    yukino "..."
    voice "audio/voice/E2/YUK/03/HA/HA006.ogg"
    hachiman "Because they're our closest strangers. Of course we're different from each other. There's no need to force yourself to change."
    show yukino happy with dissolve
    voice "audio/voice/E2/YUK/03/YK/YK008.ogg"
    yukino "..."
    show yukino vhappy with dissolve
    voice "audio/voice/E2/YUK/03/YK/YK009.ogg"
    yukino "Komachi-san likes you very much."
    voice "audio/voice/E2/YUK/03/HA/HA007.ogg"
    hachiman "Is that so?"
    show yukino angry with dissolve
    voice "audio/voice/E2/YUK/03/YK/YK010.ogg"
    yukino "Liking someone and admiring someone are different things."
    voice "audio/voice/E2/YUK/03/HA/HA008.ogg"
    hachiman "\"Liking and admiring\", huh..."
    show yukino happy1 with dissolve
    voice "audio/voice/E2/YUK/03/YK/YK011.ogg"
    yukino "Yes."
    show yukino vhappy with dissolve
    "With that short reply, Yukinoshita quietly smiles at me. Like a flower that softly blossoms in the winter."
    voice "audio/voice/E2/YUK/03/YK/YK012.ogg"
    menu yukino_siblings_reason:
        yukino "I feel like I understand why Komachi-san likes you."
        with dissolve
        "...Really?":
            "not finished"
            voice "audio/voice/E2/YUK/03/HA/HA009.ogg"
            hachiman "... Really?"
            voice "audio/voice/E2/YUK/03/YK/YK013.ogg"
            yukino ""
            voice "audio/voice/E2/YUK/03/HA/HA010.ogg"
            hachiman ""
            jump yukino_siblings_reason
        "Of course":
            "not finished"
            voice "audio/voice/E2/YUK/03/HA/HA011.ogg"
            hachiman "Of course."
            #voice "audio/voice/E2/YUK/03/YK/YK010.ogg"
            #yukino ""
            jump yukino_siblings_reason
        "...Reason?":
            voice "audio/voice/E2/YUK/03/HA/HA015.ogg"
            hachiman "... Reason?"
            show yukino happy with dissolve
            voice "audio/voice/E2/YUK/03/YK/YK019.ogg"
            yukino "Yes. After all, you and Komachi-san are completely different."
            voice "audio/voice/E2/YUK/03/HA/HA016.ogg"
            hachiman "Is that... a reason?"
            "I kind of understood what Yukinoshita meant, but I just asked anyway."
            show yukino vhappy with dissolve
            voice "audio/voice/E2/YUK/03/YK/YK020.ogg"
            yukino "Yes. Just like how you like Komachi-san who's completely different to you, I think she likes you the same."
            voice "audio/voice/E2/YUK/03/HA/HA017.ogg"
            hachiman "I see..."
            voice "audio/voice/E2/YUK/03/YK/YK021.ogg"
            yukino "Yes..."
            play sound "audio/sfx/SE03/SE03_00L.ogg" loop
            $renpy.pause(delay=1, hard=False)
            show yukino surprised with dissolve
            voice "audio/voice/E2/YUK/03/HA/HA018.ogg"
            hachiman "Ah?"
            stop sound
            play sound "audio/sfx/SE03/SE03_01.ogg"
            $renpy.pause(delay=2.0, hard=False)
            voice "audio/voice/E2/YUK/03/HA/HA019.ogg"
            hachiman "It's Komachi. \"Onii-chan, go get some ice cream\", she says."
            show yukino vhappy with dissolve
            voice "audio/voice/E2/YUK/03/YK/YK022.ogg"
            yukino "You're being depended on, \"Onii-chan\"."
            voice "audio/voice/E2/YUK/03/HA/HA020.ogg"
            hachiman "This is being a gofer though..."
            "Yukinoshita was strangely smiling carefreely. I was subconciously drawn to that smile. And then-"
            hachiman "(... Well, whatever...)"
            window auto hide dissolve
            stop music fadeout 1.0
            jump E3_CMM_01